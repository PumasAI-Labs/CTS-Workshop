Supplementary Model control files
PK model
;CMT=1 is PK, CMT=3 is dose
$PROBLEM runX.mod
$INPUT ID Cycle Cohort DAY TAFD=TIME Result=DV AMT CMT TDD BLQFLG MDV EVID Weight Age RACE PFMECOG BASTC BBILC liver 
$DATA PK-NM-cov-20201005.csv IGNORE=@ 
$SUBROUTINE ADVAN6 TOL=6
$MODEL      COMP=(PKDEPOT,DEFDOSE)
            COMP=(PERI)
            COMP=(DEPOT)

$PK
CLF = THETA(1) * EXP(ETA(1))
VF   = THETA(2) * EXP(ETA(2))
Q = THETA(3) * EXP(ETA(3))
V2 = THETA(4) * EXP(ETA(4))
KA   = THETA(5)*EXP(ETA(5))
F3S = THETA(6)*EXP(ETA(6))
F3 = 1
IF (TIME.LT.24.AND.TDD.LE.6) F3 = F3S  ;TDD refers to total daily dose, F3S is scaling factor

; Initial cond for PK compartments
A_0(1) = 0
A_0(2) = 0
A_0(3) = 0

$DES
IF (F3.GE.1) F3 = 1
DADT(1) = KA*A(3) - CLF*A(1)/VF - Q*(A(1)/VF - A(2)/V2)
DADT(2) = Q*(A(1)/VF - A(2)/V2)
DADT(3) = -KA*A(3)

$ERROR

CX = A(1)/VF            ;Drug conc (ug/L or ng/mL)
 IPRED = CX
 Y = IPRED*EXP(ERR(1))
 
$THETA  (0,9.5)      ; TVCLF ; 1
$THETA  (0, 135)  ; TVVF    ; 2
$THETA  (0, 26)  ; TVQ ; 3
$THETA  (0, 235)  ; TVV2 ; 4
$THETA  (0,2)    ; TVKA ; 5
$THETA  (0,0.5,1)  ;TVF3S ;6


$OMEGA BLOCK(2)
0.110  ; IIV.CLF    ;          1
0.01   ; COV.ETA.CLF.VF     ;        1-2
0.1    ; IIV.VF ;          2
$OMEGA  0  FIX  
$OMEGA  0  FIX
$OMEGA  0 FIX
$OMEGA  0.1 ; IIV.F3     ; 6

$SIGMA  0.09    ;ERR.PK   ;1

$ESTIMATION METHOD=1 INTERACTION MAXEVAL=9999
            PRINT=5 NOABORT SIGL=6 NSIG=2 
            NOOMEGABOUNDTEST NOSIGMABOUNDTEST MSFO=runX.MSF
$COV PRINT=E

$TABLE      ID Cycle Cohort MDV DAY TDD TAFD Result AMT CLF VF KA Q V2 F3 F3S
            IPRED CWRES CWRESI PRED  NOPRINT ONEHEADER FILE=runX.tab

$TABLE ID CLF VF KA Q V2 ETA1 ETA2 ETA3 Weight Age RACE PFMECOG BASTC BBILC liver ONEHEADER NOAPPEND FIRSTONLY NOPRINT FILE=PATAB_X


SMDA PD model
; AMT[=] ug; V[=]L; CL=L/h ; conc[=] ug/L or ng/mL
;DV=LN(Result)
$PROBLEM runX.mod
$INPUT  Cohort cycle TDD ID TIME Result DV MDV CMT AMT EVID CLFP VFP KAP QP V2P F3P
; CLFP VFP KAP QP V2P F3P are individual parameter estimate from PK model
$DATA PK-NM-SDMA-20201005_pp.csv IGNORE=@ 
IGN(CMT.EQ.1)
$SUBROUTINE ADVAN6 TOL=6
$MODEL      COMP=(PKDEPOT,DEFDOSE)
            COMP=(PERI)
            COMP=(DEPOT)
            COMP=(SDMA)

$PK

; SDMA parameters
IMAX = THETA(1)*EXP(ETA(1))
IC50 = THETA(2)*EXP(ETA(2))
KOUT = THETA(3)*EXP(ETA(3))
SDMA0 = THETA(4)*EXP(ETA(4))
KIN = SDMA0*KOUT

; CLFP VFP KAP QP V2P F3P are individual parameter estimate from PK model
CLF = CLFP
VF = VFP
Q = QP
V2 = V2P
KA = KAP
F3 = F3P

; Initial cond for SDMA comp
A_0(4) = SDMA0

$DES
;PK comp
DADT(1) = KA*A(3) - CLF*A(1)/VF - Q*(A(1)/VF - A(2)/V2)
DADT(2) = Q*(A(1)/VF - A(2)/V2)
DADT(3) = -KA*A(3)

; SDMA comp
DADT(4) = KIN*(1- IMAX*A(1)/VF / (IC50 + A(1)/VF) ) - KOUT*A(4)

$ERROR
CS = A(4)           ;SDMA conc (ng/mL)
IPRED = LOG(CS+0.00001)  ;SDMA modeled on log-transformed data; Normal(log(u), sigma)
 Y = IPRED +ERR(1) ;additive error model for SDMA

$THETA  (0,0.9,1)   ; TV.IMAX  ; 1
$THETA  (0,2)       ; TV.IC50  ; 2
$THETA  (0,0.03)    ; TV.KOUT  ; 3
$THETA  (0,120)     ; TV.SDMA0 ; 4

$OMEGA  
0 FIX   ; IIV. IMAX ;1
0 FIX   ; IIV. IC50 ;2
0 FIX   ;IIV.KOUT   ; 3
0.1    ;IIV.SDMA0   ; 4

$SIGMA  0.1     ;ERR.SDMA   ;1

;$SIMULATION (1 NEW) ONLYSIM SUB=1
$ESTIMATION METHOD=1 INTERACTION MAXEVAL=9999
            PRINT=5 NOABORT SIGL=6 NSIG=2 
            NOOMEGABOUNDTEST NOSIGMABOUNDTEST MSFO=runX.MSF
$COV PRINT=E

$TABLE      ID Cycle Cohort MDV CMT TDD TIME DV AMT CLF VF KA Q V2 F3 
            IMAX IC50 KOUT SDMA0
            CS CWRES CWRESI PRED NOPRINT ONEHEADER FILE=runX.tab

$TABLE Cohort TDD ID TIME CMT CLF VF KA Q V2 F3 
            IMAX IC50 KOUT SDMA0 CS MDV CMT ETA4 ONEHEADER NOAPPEND FIRSTONLY NOPRINT FILE=PATAB_X  



Platelet count model 
;CMT=8 is PLT
$PROBLEM runX.mod
$INPUT  Cohort cycle TDD ID TAFD=TIME Result=DV MDV CMT AMT EVID CLFP VFP KAP QP V2P F3P
; CLFP VFP KAP QP V2P F3P are individual parameter estimate from PK model

$DATA PK-NM-SDMA-PLT-20201005_pp.csv IGNORE=@ 
IGN(CMT.EQ.1)
$SUBROUTINE ADVAN6 TOL=6
$MODEL      COMP=(PKDEPOT,DEFDOSE)
            COMP=(PERI)
            COMP=(DEPOT)
            COMP=(PROL)
            COMP=(TRN1)
            COMP=(TRN2)
            COMP=(TRN3)
            COMP=(CIRC,DEFOBS)

$PK

MTT   = THETA(1) * EXP(ETA(1))
KTR = 4/MTT
KCIRC = KTR
KPROL = KTR

SLOPE = THETA(2) * EXP(ETA(2))

GAM = THETA(3) * EXP(ETA(3))
BASE = THETA(4) * EXP(ETA(4))

; CLFP VFP KAP QP V2P F3P are individual parameter estimate from PK model
CLF = CLFP
VF = VFP
Q = QP
V2 = V2P
KA = KAP
F3 = F3P

; Initial cond for PD compartments
A_0(4) = (KCIRC*BASE) / KTR
A_0(5) = (KCIRC*BASE) / KTR
A_0(6) = (KCIRC*BASE) / KTR
A_0(7) = (KCIRC*BASE) / KTR
A_0(8) = BASE

$DES
; PK 
DADT(1) = KA*A(3) - CLF*A(1)/VF - Q*(A(1)/VF - A(2)/V2)
DADT(2) = Q*(A(1)/VF - A(2)/V2)
DADT(3) = -KA*A(3)
; Proliferative cells comp
DADT(4) = KPROL * A(4) * (1-SLOPE* A(1)/VF)*(BASE/A(8))**GAM - KTR*A(4)
; Transit comp
DADT(5) = KTR*A(4) - KTR*A(5)
DADT(6) = KTR*A(5) - KTR*A(6)
DADT(7) = KTR*A(6) - KTR*A(7)
; Circulating comp
DADT(8) = KTR*A(7) - KCIRC*A(8)

$ERROR
CIRC = A(8)             ;platelet count (10^9/L)

IPRED = CIRC
Y = IPRED*EXP(ERR(1))  ; exponential error

$THETA  (0, 100)     ; TV.MTT   ; 1
$THETA  (0, 0.003)   ; TV.SLOPE ; 2
$THETA  (0, 0.1)     ; TV.GAM ; 3
$THETA  (0, 200)     ; TV.BASE ; 4

$OMEGA 0 FIX     ; IIV.MTT ; 1
$OMEGA BLOCK(3)                       
0.1    ; IIV.SLOPE  ; 2
0.01   ; COV.ETA.SLOPE.GAM ;2-3
0.1    ; IIV.GAM    ; 2
0.01   ; COV.ETA.SLOPE.BASE ;2-4
0.01   ; COV.ETA.GAM.BASE ;3-4
0.1    ; IIV.BASE   ; 4

$SIGMA  0.09     ;ERR.PD   ;1 

$ESTIMATION METHOD=1 INTERACTION MAXEVAL=9999
            PRINT=5 NOABORT SIGL=6 NSIG=1 
            NOOMEGABOUNDTEST NOSIGMABOUNDTEST MSFO=runX.MSF
$COV  PRINT=E

$TABLE      ID Cycle Cohort MDV CMT TDD TAFD Result AMT CLF VF KA Q V2 F3
            MTT SLOPE GAM BASE CIRC
            CWRES CWRESI PRED  NOPRINT ONEHEADER FILE=runX.tab

$TABLE ID CLF VF KA Q V2 F3 MTT SLOPE GAM BASE CIRC ETA1 ETA2 ETA3 ETA4 ONEHEADER NOAPPEND FIRSTONLY NOPRINT FILE=PATAB_X


